plugins {
    id "com.jfrog.bintray" version "1.1"
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

archivesBaseName = 'configur8'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = releaseVersion
group = 'io.github.daviddenton'

description = 'Nano-library which provides the ability to define typesafe (!) configuration templates for applications.'

dependencies {
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'configur8'

            pom.withXml {
                asNode().appendNode('name', 'configur8')
                asNode().appendNode('description', description)
                asNode().appendNode('url', 'http://configur8.github.io')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('name', 'David Denton').parent().
                        appendNode('email', 'mail@daviddenton.github.io')
                asNode().appendNode('scm').
                        appendNode('url', 'git@github.com:daviddenton/configur8.git').parent().
                        appendNode('connection', 'scm:git:git@github.com:daviddenton/configur8.git').parent().
                        appendNode('developerConnection', 'scm:git:git@github.com:daviddenton/configur8.git')
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache License, Version 2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html')
            }
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = 'daviddenton'
    key = project.bintrayKey

    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'configur8'

        desc = description
        websiteUrl = 'https://github.com/daviddenton/configur8'
        issueTrackerUrl = 'https://github.com/daviddenton/configur8/issues'
        vcsUrl = 'https://github.com/daviddenton/configur8.git'
        licenses = ['Apache-2.0']
        labels = []
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
